-- Top level contract declaration
DContr. Contr ::= "contract" Name "(" [Pattern] ")" "=" "{" Expr "}" ;

token Name (upper (letter | digit | '_' | '\'')*) ;
token Var (lower (letter | digit | '_' | '\'')*) ;

separator nonempty Var "," ;
separator nonempty Name "," ;

-- Expressions
ENil.    Expr4 ::= "Nil" ;
EValue.  Expr4 ::= Value ;
EVar.    Expr4 ::= Var ;
EDrop.   Expr3 ::= "*" Expr3 ;
EQuote.  Expr3 ::= "@" Expr3 ;
EInject. Expr3 ::= "#" Expr3 ;
ELift.   Expr2 ::= Expr3 "!" "(" [Expr] ")" ;
EInput.  Expr1 ::= "for" "(" [Bind] ")" "{" Expr "}" ;
EChoice. Expr1 ::= "select" "{" [CBranch] "}" ;
EMatch.  Expr1 ::= "match" Expr "with" [PMBranch] ; -- should be Expr instead of Var
ENew.    Expr1 ::= "new" [Var] "in" Expr1 ;
EConstr. Expr1 ::= Name "(" [Expr] ")" ; -- should be Expr instead of Var
EPar.    Expr  ::= Expr "|" Expr1 ;
separator nonempty Expr "," ; -- Might make sense to not have this nonempty, to allow pure synchronization?
coercions Expr 4 ;

-- Pattern match branches
PatternMatch. PMBranch ::= Pattern "=>" "{" Expr "}" ;
separator nonempty PMBranch "" ; 

-- Patterns
PtWild.   Pattern4 ::= "_" ;
PtNil.    Pattern4 ::= "Nil" ;
PtVar.    Pattern4 ::= Var ;
PtVal.    Pattern4 ::= VPattern ;
PtDrop.   Pattern3 ::= "*" Pattern3 ;
PtInject. Pattern3 ::= "#" Pattern3 ;
PtQuote.  Pattern3 ::= "@" Pattern3 ;
PtOutput. Pattern2 ::= Pattern3 "!" "(" [Pattern] ")" ;
PtInput.  Pattern1 ::= "for" "(" [PatternBind] ")" "{" Pattern "}" ;
PtMatch.  Pattern1 ::= "match" Pattern "with" [PatternPatternMatch] ;
PtConstr. Pattern1 ::= Name "(" [Pattern] ")" ;
--PtString. Pattern ::=  ;
--PtArray.  Pattern ::=  ;
--PtList.   Pattern ::=  ;
PtPar.    Pattern ::= Pattern "|" Pattern1 ;
separator nonempty Pattern "," ;
coercions Pattern 4 ;

PtBind.   PatternBind ::= Pattern "<-" Pattern ;
separator nonempty PatternBind ";" ;

PtBranch. PatternPatternMatch ::= Pattern "=>" "{" Pattern "}" ;
separator nonempty PatternPatternMatch "" ;


VPtStruct. VPattern ::= Var "{" [Pattern] "}" ;

Choice. CBranch ::= "case" [Bind] "=>" "{" Expr "}" ;
separator nonempty CBranch "" ;

-- Variable binding
Bind. Bind ::= Pattern "<-" Expr ;
separator nonempty Bind ";" ;

VQuant.   Value    ::= Quantity ;
VEnt.     Value    ::= Entity ;
-- QBool.    Quantity ::= Boolean ;
QInt.     Quantity ::= Integer ;
QDouble.  Quantity ::= Double ;
EChar.    Entity   ::= Char ;
-- EDate.    Entity   ::= Datetime ;
EStruct.  Entity   ::= Struct ;
ECollect. Entity   ::= Collect ;

Struct. Struct ::= Var "{" [Expr] "}" ;

CString. Collect ::= String ;
-- CArray.  Collect ::= Array ;
-- CList.   Collect ::= List ;