
-- Top level contract declaration
DContr. Contr ::= "contract" Name "(" [Arg] ")" "=" "{" Proc "}" ;

separator nonempty Ident "," ;

VarIdent.  Var  ::= Ident ;
NameIdent. Name ::= Ident ;

separator nonempty Var "," ;
separator nonempty Name "," ;

-- Processes
PNil.    Proc ::= "Nil" ;
PInput.  Proc ::= "for" "(" [Bind] ")" "{" Proc "}" ;
PChoice. Proc ::= "select" "{" [CBranch] "}" ;
PMatch.  Proc ::= "match" Proc "{" [PMBranch] "}" ; -- should be Proc instead of Var
POutput. Proc ::= Chan "!" "(" [Proc] ")" ; -- should be Proc instead of Var
PPar.    Proc ::= Proc "|" Proc ;
PDrop.   Proc ::= "*" Chan ;
PNew.    Proc ::= "new" [Var] "in" Proc ;
PConstr. Proc ::= Name "(" [Proc] ")" ; -- should be Proc instead of Var
PValue.  Proc ::= Value ;
PVar.    Proc ::= Var ; -- induces reduce/reduce conflicts :(
separator nonempty Proc "," ; -- Might make sense to not have this nonempty, to allow pure synchronization?

ArgPattern. Arg ::= Pattern ;
ArgVar.     Arg ::= Var ;
separator nonempty Arg "," ;

PatternMatch. PMBranch ::= Pattern "=>" "{" Proc "}" ;
separator nonempty PMBranch "" ; 

-- Pattern matching
PtNil.    Pattern ::= "Nil" ;
--PtInput.  Pattern ::= "for" ;
PtOutput. Pattern ::= Chan "!" "(" [Pattern] ")" ;
PtPar.    Pattern ::= Pattern "|" Pattern ;
PtDrop.   Pattern ::= "*" Chan ;
PtConstr. Pattern ::= Name "(" [Pattern] ")" ;
PtVal.    Pattern ::= VPattern ;
PtMatch.  Pattern ::= "match" Pattern "{" [Ident] "}" ; -- does this really make sense?
separator nonempty Pattern "," ;

-- VPtString. VPattern ::=  ;
-- VPtArray.  VPattern ::=  ;
-- VPtList.   VPattern ::=  ;
VPtStruct. VPattern ::= Name "{" [Proc] "}" ; -- second Ident should be Proc

Choice. CBranch ::= "case" [Bind] "=>" "{" Proc "}" ;
separator nonempty CBranch "" ;

-- Variable binding
Bind. Bind ::= Arg "<-" Chan ;
separator nonempty Bind ";" ;

-- Channels
ChanAddr. Chan ::= Addr ;
ChanVar.  Chan ::= Var ;

-- AddrHash. Addr ::= Hash ;
AddrLift. Addr ::= "@" Proc ;

VQuant.   Value    ::= Quantity ;
VEnt.     Value    ::= Entity ;
-- QBool.    Quantity ::= Boolean ;
QInt.     Quantity ::= Integer ;
QDouble.  Quantity ::= Double ;
EChar.    Entity   ::= Char ;
-- EDate.    Entity   ::= Datetime ;
EStruct.  Entity   ::= Struct ;
ECollect. Entity   ::= Collect ;

Struct. Struct ::= Name "{" [Proc] "}" ;

CString. Collect ::= String ;
-- CArray.  Collect ::= Array ;
-- CList.   Collect ::= List ;